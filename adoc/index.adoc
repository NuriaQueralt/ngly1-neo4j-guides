:author: Núria Queralt Rosinach
:twitter: nqueralt
:tags: NGLY1 Deficiency, NGLY1, Hypothesis Generation
:neo4j-version: 3.5.3
:icons: font
//:imagesdir: ./images


[#img-app]
.NGLY1 Deficiency Knowledge Graph application. Go to the *Wikibase site* by clicking the application logo.
[link=http://ngly1graph.org/contribute/]
image::https://www.dropbox.com/s/rkq3595pkkvflot/ngly1_app_logo.png?dl=1[float=center]


{sp}+


== NGLY1 Deficiency Knowledge Graph in Neo4j

The NGLY1 Deficiency Knowledge Graph is the first knowledge network for the https://rarediseases.info.nih.gov/diseases/12315/deficiency-of-n-glycanase-1[Deficiency of N-glycanase 1 or NGLY1 Deficiency], which is an ultra-rare genetic disease with neither a treatment nor a clear understanding of the underlying biology. It is a *living structured review* that summarizes the current knowledge and data on the NGLY1 Deficiency in a modern and computable fashion represented as a single graph for exploration and discovery. Knowledge comes from biocuration of the scientific literature and retrieval from biomedical knowledge bases. This knowledge is integrated with omics data from the fruit fly (https://en.wikipedia.org/wiki/Drosophila_melanogaster[_Drosophila melanogaster_]) model, an animal system that has helped the community understand a wide range of human conditions, from alcoholism to Alzheimer's disease and Rett's syndrome (http://modencode.sciencemag.org/drosophila/introduction[_Drosophila_ as a model organism]). The fruit fly genetics and biology are also used in the Model Organisms Screening Center for the https://undiagnosed.hms.harvard.edu/research/model-organisms-phase-ii/[Undiagnosed Diseases Network] to tackle rare and undiagnosed diseases. The parallels between the genomes of Drosophila and humans, over about 60%, provides an opportunity to reveal basic genes or processes associated with disease. 



This is the *Neo4j Web interface* for the NGLY1 Deficiency Knowledge Graph to visualize, run queries and explore the structured reviewed knowledge and data. This application provides read-only access to the graph. To contribute new information to the graph, the user should refer to http://ngly1graph.org/contribute/[NGLY1 Deficiency Knowledge Graph in Wikibase] site of the project, a collaborative and community-editable version of the graph. _Note that_ this is a living graph because users community can build and curate it in Wikibase. For that reason, content statistics, data model and screenshots in the guide may differ with the current time evolving graph. The initial graph version v3.2 contains *234,714* relationships of 29 types among *9,359* nodes of 8 types. 

{sp}+

//=== Explore reviewed knowledge for hypothesis discovery

=== DISCOVER FROM THE STRUCTURED KNOWLEDGE-DATA REVIEWED

The NGLY1 Deficiency Knowledge Graph is a network of biological and clinical relations offered as a knowledge resource to the research community to enable discovery on its cause and of its symptoms. The network is disease domain specific to NGLY1 Deficiency and can be used for different research questions from the analysis of disease gene functions, the investigation of the genetic basis and underlying molecular biology of disease phenotypes, to the discovery of hypothesis or explanations linking concepts in the graph. Examples of the kind of questions the user can formulate are:

****
    1. What are the most common gene functions, gene interactions, cellular processes or phenotypes associated with the perturbed gene expression in Drosophila?
    2. What are the human homologs related to the perturbed transcriptome in the NGLY1 Deficiency fly model?
    3. What are the most common human Transcription Factors (TFs) associated with the genes in the fly transcriptome?
    4. Have the fly transcriptome homolog genes in human or their gene interactors been associated with NGLY1 Deficiency phenotypes?
    5. Can we retrieve explanations or paths linking two entities of interest such as two genes? What are the most common path topologies or metapaths linking these genes? Are there paths based on regulatory TF-target gene interactions in the structured review?
****

The knowledge graph can be queried in almost an infinite number of ways. This guide will demonstrate two such queries as templates for you to customize.

{sp}+

=== DATA MODEL


The figure 1 shows the data model or schema or meta-graph for NGLY1 Deficiency Knowledge Graph v3.2. Click the figure to zoom out. 


[#img-schema]
.NGLY1 Deficiency Knowledge Graph v3.2 data model
[link=https://www.dropbox.com/s/6m1rfik5qqftyvi/ngly1kg_metagraph_2019-03-12.png?dl=0]
image::https://www.dropbox.com/s/6m1rfik5qqftyvi/ngly1kg_metagraph_2019-03-12.png?dl=1[float=center]
//My image dir path is {imagesdir} 

//image::metagraph.png[height=300,float=center]
//image::ngly1kg_metagraph_2019-03-12.png[height=300,float=center]
//image::https://github.com/NuriaQueralt/ngly1-neo4j-guides/blob/master/guides/images/ngly1kg_metagraph_2019-03-12.png[height=300,float=center]
//image::https://github.com/NuriaQueralt/ngly1-neo4j-guides/commit/30d901927193d59f624409bab8d329c91aa873a1?dl=1[height=300,float=center]



For an interactive description of the data model, run the following query:

[source,cypher]
----
CALL db.schema()
----

{sp}+

==== Data types


The network relates data of 8 types: *genes* (`GENE`), *disease phenotypes* (`DISO`), *variants* (`VARI`), *genotypes* (`GENO`), *pathways* (`PHYS`), *chemicals* (`CHEM`), *anatomy* (`ANAT`), and *organisms* (`LIVB`) with different levels of association expressiveness (29 edge types). Data and relations are described using standard semantics: community-driven ontologies and controlled vocabularies to enable interoperability with external resources and reduce ambiguity. To see the frequency of node types, run the following query:

[source,cypher]
----
MATCH (n) RETURN DISTINCT labels(n), COUNT(DISTINCT n.id) AS nodes ORDER BY nodes DESC
----

{sp}+

The semantics of the concepts in the graph rely on the semantic types that build the https://www.nlm.nih.gov/pubs/factsheets/umlssemn.html[UMLS Semantic Network]. To check the definition of each semantic type abbreviation see the https://metamap.nlm.nih.gov/Docs/SemGroups_2013.txt[UMLS Semantic Groups definitions]. The semantics of the relations rely basically on the http://www.obofoundry.org/ontology/ro.html[Relation Ontology (RO)]. To see the frequency of semantic relation types, run the following query:

[source,cypher]
----
MATCH path=(n)-[r]->(m) RETURN DISTINCT type(r) AS id, r.property_label AS label, COUNT(DISTINCT path) AS paths ORDER BY paths DESC
----

{sp}+

==== ID scheme


The semantics of the data instances rely on 87 different specific ontologies and vocabularies in function of the concept type:

.Relation of data instance and ID scheme namespaces used
|===
| *Node type* | *ID scheme* 
| Genes | HGNC, NCBI Gene, FlyBase, ZFIN, MGI, RGD, XenBase, WormBase, UniProt, SGD, Ensembl, React, InterPro, EC, GO
| Diseases phenotypes | MP, FBbt, HP, ZP, WBPhenotype, FBcv, AQTLTrait, MONDO, OMIM, DOID, APO, Orphanet 
| Variants | ClinVar, SO
| Genotype | Monarch, Coriell, MMRRC
| Pathways | GO, Reactome, KEGG, MESH, CHEBI, FOODON, HP
| Chemicals | ChEBI, DrugBank, HMDB, UMLS
| Anatomy | UBERON, CL, GO, EDAM
| Organism | NCBITaxon
|===




{sp}+

==== Data attributes


The graph data are created as two different data structures: nodes and edges. Their data attributes are:

* *Node*
    - `id`: Entity identifier in an domain specific ontology or controlled vocabulary.
    - `preflabel`: Preferred human-readable label for the entity. In case of genes, this is the gene symbol.
    - `name`: Common known term used by the community to refer to the entity.
    - `description`: Short sentence describing the entity. 

* *Edge*
    - `property_label`: Preferred human-readable label for the property or type of association in the edge.
    - `property_uri`: https://en.wikipedia.org/wiki/Uniform_Resource_Identifier[Uniform Resource Identifier (URI)] to identify the property. URIs are the Web standard identifier and allow accessing to metadata by dereferrencing them, and linking to other data in the Web. 
    - `reference_uri`: URI to identify the reference where the association (edge) is claimed, i.e. the provenance.
    - `reference_supporting_text`: Sentence extracted from the reference where the edge is stated, i.e. the evidence.
    - `reference_date`: Date of publication of the reference of the edge.


_NOTE_: The attribute(s) do not appear in the graphical interface when the value is null.


{sp}+



IMPORTANT:  Click the top right **black&white** arrow to go to the next page


== Data 


The NGLY1 Deficiency Knowledge Graph reviews heterogeneous **knowledge** from basic biological and clinical research in humans and derived from animal models, and **data** generated from high-throughput experiments. The integrated knowledge and data comes from different external sources including scientific literature, knowledge bases and experimental omics data sets and are annotated to its provenance and evidence. The integration is performed by means of vocabulary mapping and normalization. We provide a computational tool for the reproducible generation workflow of the graph, a https://github.com/NuriaQueralt/graph-hypothesis-generation-lib[Python library] where each data source has a specific module to prepare edges as a network to be integrated into the NGLY1 Deficiency Knowledge Graph schema.


We reviewed and structured _knowledge_ with its evidence around the clinical profile and the pathophysiology of the NGLY1 Deficiency to help biomedical researchers to discover evidence-based explanations for their hypotheses on its cause and of its symptoms. We assembled **human** diverse knowledge about basic molecular biology, cys-regulation, and clinical disease phenotypes extracted by biocuration of publications in scientific journals, retrieved from databases, and knowledge from domain experts. We derived knowledge from **animal models** assembling homologs and ortholog-phenotypes. This knowledge is complemented and gives structure to new experimental transcriptomics _data_ from recent studies.



{sp}+


=== ORIGINAL DATA SOURCES



The third release of the graph organizes information as edges from human and animal models 1) manually extracted from investigations published in https://www.ncbi.nlm.nih.gov/pubmed/[PubMed]; 2) retrieved from https://monarchinitiative.org/[the Monarch Initiative]; 3) transcription factor regulation from several sources in https://github.com/slowkow/tftargets[tftargets] and the http://software.broadinstitute.org/gsea/msigdb[Molecular Signatures Database or MSigDB]; 4) manually contributed by the community from http://100.25.145.12:8181/wiki/Main_Page[the NGLY1 Deficiency wikibase platform]; and integrates all this heterogeneous knowledge with an experimental gene expression data set from a fruit fly _Drosophila melanogaster_ model of the disease https://www.ncbi.nlm.nih.gov/pubmed/29346549[Owings _et al_.]. We have ingested existing data into a knowledge graph creating first several networks, which type of content is:

* Knowledge:
    - Human heterogeneous edges: curation, Monarch and regulation networks
    - Animal model homologs and ortholog-phenotypes edges: Monarch network

* Data:
    - Omics edges: transcriptomics network


{sp}+

Networks are assembled and organized according the types and source of edges:


==== Curation Network
Data come from manual curation. We manually curated https://www.ncbi.nlm.nih.gov/pubmed/24651605[Enns _et al_., 2014] and https://www.ncbi.nlm.nih.gov/pubmed/27388694[Lam _et al_., 2017] publications to capture the known pathophysiology and clinical phenotypic spectrum of the disease. From free text in Lam _et al_., we structured around 100 associated disease phenotypes and formalized them into the HPO. Data are retrieved from https://github.com/NuriaQueralt/graph-hypothesis-generation-lib/tree/master/plan/curation/data/v20180118[tabulated files] generated by the biocurator.


==== Monarch Network
Data come from the Monarch Initiative Knowledge Base. To translate genotype and phenotype, the knowledge base semantically integrates animal model with human data such as diseases, genes, variants, or phenotypes. They are retrieved through the BioLink API. Data are obtained for specific seed nodes requested by NGLY1 Deficiency researchers using the https://github.com/NuriaQueralt/graph-hypothesis-generation-lib/blob/master/plan/graph_v3.2_v20190312.ipynb[workflow]. 


==== Regulation Network 
Data come from the tftargets and the MSigDB databases and consist of known human transcription factor and target genes associations. 

**tftargets** R package provides TF-genes associations data from 6 databases with different type of evidence. We retrieved only data from TRED, ENCODE, Neph2012 and TRRUST databases because they have reliable curated or experimental evidence. Data are retrieved from the GitHub tftargets repository as a bulk download. Data come from the https://github.com/slowkow/tftargets/blob/master/data/tftargets.rda[tftargets.rda files] and provenance from the https://github.com/slowkow/tftargets/blob/master/data-raw/TRRUST/trrust_rawdata.txt.gz[TRRUST raw data file].

**MSigDB** is a collection of annotated gene sets for use with GSEA software. Data are retrieved from the MSigDB web site as a bulk download. We use the C3:TFT gene set collection from the https://github.com/NuriaQueralt/graph-hypothesis-generation-lib/blob/master/plan/regulation/msigdb/data/c3.tft.v6.1.entrez.gmt[entrez genes ids GMT file]. 


==== Transcriptomics Network
Data come from an RNA-seq experiment on the perturbed transcriptome in a fly model of NGLY1 Deficiency https://www.ncbi.nlm.nih.gov/pubmed/29346549[Owings _et al_., 2018]. It contains the differential expression at cutoffs of absolute fold change > 1.5 and false discovery rate < 0.05. Data are retrieved from data authors by request. Data are obtained from the https://github.com/NuriaQueralt/graph-hypothesis-generation-lib/blob/master/plan/transcriptomics/ngly1-fly-chow-2018/data/supp_table_1.csv[CSV file].


{sp}+

=== DATA STATISTICS


In the following table we show the dimensions of each network before integration into the graph, and the dimensions of the final NGLY1 Deficiency Knowledge Graph (KG) and the statistics of each data source finally integrated.

{sp}+

.Data statistics per network
[cols=",,",frame=topbot,grid=rows]
|===
|*Source* | *Nodes* | *Edges*
|*_Before integration_* | |
|Curation | 302 | 362
|Monarch | 4,644 | 32,715
|Regulation | 16,963 | 197,267
|Transcriptomics | 386 | 386
|*_After integration_* | |
|NGLY1 Deficiency KG | 9,365 | 237,027
|Curation | 302 | 362
|Monarch | 4,644 | 226,556
|Regulation | 4,226 | 9,723
|Transcriptomics | 386 | 386
|===


{sp}+

=== DATA AVAILABILITY
Files used to build the NGLY1 Deficiency Knowledge graph v3.2 are available at:

- Curation network files: https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/curated_graph_nodes_v2019-01-18.csv[_nodes_]; https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/curated_graph_edges_v2019-01-18.csv[_edges_]
- Monarch network files: https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/monarch_nodes_v2019-01-18.tsv[_nodes_]; https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/monarch_edges_v2019-01-18.tsv[_edges_]
- Regulation network files: https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/regulation_nodes_v2019-01-17.csv[_nodes_]; https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/regulation_graph_edges_v2019-01-17.csv[_edges_]
- Transcriptomics network files: https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/rna_nodes_v2019-01-17.csv[_nodes_]; https://github.com/NuriaQueralt/ngly1-graph/blob/master/regulation/graph/rna_edges_v2019-01-17.csv[_edges_]
- NGLY1 Deficiency Knowledge Graph files: https://github.com/NuriaQueralt/ngly1-graph/tree/master/neo4j-graphs/ngly1-v3.2/import/ngly1[_network files_]


{sp}+

IMPORTANT: Click the top right **black&white** arrow to go to the next page



== Explore reviewed knowledge for hypothesis discovery
//== QUERY 1: HYPOTHESIS DISCOVERY RELATING NGLY1 - AQP1 


In the following slides, we will query the graph to showcase how to explore this heterogeneous knowledge to extract potential hypotheses focusing on a specific research question.


{sp}+

=== RESEARCH QUESTION


Consider a researcher wants to determine whether a novel protein Aquaporin 1 (AQP1) is related with the NGLY1 Deficiency. From the experimental observation, the researcher has identified a causal association between NGLY1 and AQP1 genes at expression level in both a mouse model and patient data, but the mechanism of that association is not known. The researcher's hypothesis is that both genes are linked through transcriptional regulation and she wants to search the knowledge and the expression data integrated in the review to find potential mechanisms and, if so, to link these mechanisms to the disease phenotypes. Here, we demonstrate how to extract potential mechanistic paths from the knowledge graph. To answer this complex question we will perform two different queries:

****
            1. First we will query for NGLY1-AQP1 genetic association evidence
            2. Then we will perform a second query to retrieve gene-disease association evidence
****


{sp}+

=== SET UP THE NEO4J BROWSER


You are currently interacting with the NGLY1 Deficiency knowledge graph through the Neo4j Browser, which provides a web interface to the database where you can run queries and explore the connected knowledge.

We first style the graph, before playing with it. To load the NGLY1 Deficiency graph style into the Neo4j Browser, run the following command by clicking the box, which will populate the shell, and hitting ```enter``` (or ```return``` in some keyboards). This command only needs to be run once per web browser.


[source,cypher]
----
:style http://ngly1graph.org:8001/guides/style.grass
----


TIP:  The user can set up the style of the graph by itself locally in its browser. If you click on any label or relationship above the graph visualization, you can then choose its styling in the area below the graph. Colors, sizes, and captions are selectable from there. To reset a style the user can execute `:style reset`. Click http://guides.neo4j.com/browser.html[Styling Neo4j Browser Visualization] for more details. 

{sp} +

To explore the knowledge graph, go first to the browser settings at the bottom of the left frame and de-select the `Connect result nodes` option to get only the explicit connecting paths. This action needs to be done every time the user opens the browser.

{sp}+


=== QUERY 1: HYPOTHESIS DISCOVERY RELATING TWO GENES
//=== Identify mechanistic links between NGLY1 and AQP1

This first query describes the situation where a link between two genes has been experimentally confirmed but the mechanism is unknown.  For example, researchers have recently identified that AQP1 and NGLY1 are functionally related. Here, we show how the NGLY1 Deficiency Knowledge Graph can be used to identify mechanistic hypotheses.

In particular, we are searching for regulatory hypotheses that link NGLY1 and AQP1 through gene regulatory interactions. To mine the gap of knowledge to identify regulatory mechanistic links, we will explore for paths that link the perturbed transcriptomic expression in a fly model of NGLY1 Deficiency through regulatory TF-target gene interactions to human AQP1. Since we are translating fly biology to human biology we will explore paths with a rule of 2-hop `gene-interacts with` to allow certain flexibility.


{sp}+

==== Define the query template


The first think to do is to formulate the query template in a structured manner to answer this question:

{sp}+

image::https://www.dropbox.com/s/qypkrn1l50p83n6/query1_topology_v3.2.png?dl=1[height=100, width=5000, align="center"]

{sp}+

In this template, we specify relations between genes to be of two types: 1) `'interacts with'` that with the use of filters on edge attributes will be explicitly either of _regulatory_ origin (once filtered by source of provenance to the databases *tftargets* and *MSigDB*), or of _gene expression_ origin (once filtered by reference to the *PubMed Identifier*, where the experimental fly transcriptome data set was published), or of a general _gene-gene interaction_ origin (if none of these restrictions are applied); and 2) `'in 1 to 1 orthology relationship with'` that relates to direct orthology between genes.


If the user does not have a clear idea for a query template, she/he could formulate an open query _a priori_ to retrieve the meta-paths of all links between two entities in the review graph. For an example of how to query the graph to retrieve meta-paths, go to the extended tutorial (find how to access the extended tutorial in the last slide).

{sp}+

==== Formulate the query in Cypher and execute it


Once we have a clear query template we can create the query in Cypher to search for mechanistic links based on gene interactions and regulation edges. The following query traverses the graph to find these kind of links in the review. The query sets the topology of the query, i.e. the meta-path or semantics or the sequence of types of nodes and edges, and the rules and constrains to fire potential explanations.




**Execute the query yourself and explore paths that relate NGLY1 and AQP1 through gene interactions**

To execute the query, click the text box below to pre-fill the shell at the top of the screen and then hit `enter` or `return` in the upper right.


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.preflabel = 'Pngl' AND target.preflabel = 'AQP1' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN path
----



{sp}+



Let's explain this query statement by statement. First, we look for mechanisms that match the query template above, where we define gene interactions with the node type _GENE_ and the edge types  _RO:0002434_ (`interacts with`) and _RO:HOM0000020_ (`in 1 to 1 orthology relationship with`):


 MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)


Then, we filter to specific source and target nodes with the _preferred label_ (`preflabel`) node attribute. The source is Pngl (FlyBase:FBgn0033050), which is the fly ortholog of the human gene NGLY1, (HGNC:17646) and the target is the human gene AQP1 (HGNC:633):

 WHERE source.preflabel = 'Pngl' AND target.preflabel = 'AQP1'


Alternatively, we could also use the _identifier_ (`id`) node attribute to filter to specific entities without ambiguity:


 WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633'


and we impose that every every node instance is traversed only once in the path using the following rule:


 AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))


We extract some features from each path to then filter them, using the 'path' variable:


 WITH path,


We extract nodes with too general meaning as _nodes_marked_:


 [ n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype'] ] AS nodes_marked,


We extract regulatory edges filtering by the `reference_supporting_text` edge provenance attribute as _interactions_:


 [ r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*' ] AS interactions


We filter paths that only content specific node instances and at least have one regulatory edge:


 WHERE size(nodes_marked) = 0 AND size(interactions) <> 0


We filter paths that the first edge, named in the path pattern as 'i1', is a transcriptomic interaction from the experimental dataset published in the PubMed id "PMID:29346549". We filter it by reference provenance using the `reference_uri` edge attribute:


 AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'


We filter paths that the fourth edge, named as 'i4', is a TF -> GENE regulatory interaction. We define a specific directionality of the interaction to keep the TF function towards AQP1. We filter it by source provenance using the `reference_supporting_text` edge attribute:


 AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'


Last, we retrieve paths:


 RETURN path


{sp}+



==== Explore hypotheses


Results can be vizualized in a summary table of a list of properties calculated from all paths or in a graph exposing all the paths. In this case we can explore all paths directly in graph format that looks like (click figures to zoom out):


[#img-q1-paths]
.Query 1 resulting hypotheses
[link=https://www.dropbox.com/s/bbdg1orj7ztrvup/query1_paths_v3.2.png?dl=0]
image::https://www.dropbox.com/s/bbdg1orj7ztrvup/query1_paths_v3.2.png?dl=1[height=10000, width=15500]

{sp}+

If we want to retrieve a summary table with for instance all genes that are differentially expressed genes of Pngl and AQP1 Transcription Factors (TFs) ordered by the total number of paths they appear such as:


[#img-q1-table]
.Query 1 summary table
[link=https://www.dropbox.com/s/v7i3ld0x3cup7nj/query1_table_v3.2.png?dl=0]
image::https://www.dropbox.com/s/v7i3ld0x3cup7nj/query1_table_v3.2.png?dl=1[height=5000, width=10000]

{sp}+

the query should be:



**Execute the query to get a summary table of gene interactors in the resulting paths**


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,rna,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN DISTINCT rna.name AS Expressed_gene_name, tf.name AS TF_name, count(distinct path) as Total_paths

ORDER BY Total_paths DESC
----

{sp}+



TIP: If the user wants to list ortholog rna genes, replace `rna` label by `ortholog_rna` in the query to refer to the third node position in the query template.



{sp}+






== Explore reviewed knowledge for hypothesis discovery (continued)

=== QUERY 2: HYPOTHESIS DISCOVERY RELATING AQP1 - DISEASE PHENOTYPES


Next question that the researcher wants to explore is the connection between the AQP1 and the NGLY1 Deficiency disease phenotypes. In particular, the researcher wants to query the review to extract paths from genes to phenotypes that link the AQP1-NGLY1 genetic association evidence obtained in the query 1 with the NGLY1 Deficiency phenotypes.


==== Define the query template

Again, the first think to do is to formulate the query template in a structured manner to answer this question:


{sp}+

image::https://www.dropbox.com/s/ch261h9xwpu5zm5/query2_topology_v3.2.png?dl=1[height=600, width=2500, align="center"]

{sp}+

The logics of this query is in two steps: 1) to collect first the genetic evidence linking NGLY1 to AQP1 via regulatory hypothesis retrieved from qurey 1, and 2) from this gene collection to retrieve those genes that have association evidence with the disease phenotypes. Consequently, in the first step we have to define again the template used in query 1, and in the second step we define a new template, where we specify a first disease-phenotype relation for the NGLY1 Deficiency disease node (*SOURCE*) and its associated phenoytpes, and a second relation from these phenotypes to the genes collected in the query 1 (*TARGET*) gene nodes.


{sp}+

==== Formulate the query in Cypher and execute it
With the query template defined, we can develop the query in Cypher. To identify and extract gene to phenotype mechanistic links we have to formulate rules in Cypher that follows the logics of the hypothesis, and execute the query. The following query traverses the graph to infer plausible associations of the AQP1 gene with the NGLY1 Deficiency phenotypes from the regulatory hypothesis. The query sets the topology of the hypothesis and the rules and constraints to fire potential explanations.


**Execute the query yourself and explore paths that relate AQP1 to disease phenotypes**

[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,ortholog_rna,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

WITH COLLECT(DISTINCT ortholog_rna.id) + COLLECT(DISTINCT tf.id) AS genes

MATCH path=(:DISO {id: 'MONDO:0014109'})-->(ph:DISO)--(g:GENE)

WHERE g.id in genes AND ph.id CONTAINS 'HP:'

RETURN path
----


{sp}+

Let’s explain this query statement by statement. First, we look for mechanisms that match the first query template above, where we define gene interactions with the node type _GENE_ and the edge types _RO:0002434_ (`interacts with`) and _RO:HOM0000020_ (`in 1 to 1 orthology relationship with`):


 MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

Then, we filter to the specific _Pngl_ source and _NGLY1_ target nodes (using the `id` attribute), and the no loops in the path rules:

 WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

To apply rules we need to extract some features from the path, using the 'path', 'ortholog_rna' and 'tf' variables:

 WITH path,ortholog_rna,tf,

We extract nodes with too general semantics as _nodes_marked_:

 [n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

We extract regulatory edges as _interactions_:

 [r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

We then filter paths that only have specific semantics and at least one regulatory TF-gene edge:

 WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

We filter paths where the first edge is a transcriptomic gene-gene interaction in the fly model:

 AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

We finally apply the filter that the last edge in the topology has to be a TF-gene regulatory interaction:

 AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

All the rules applied til this point are exactly the query 1 to retrieve the regulatory genetic evidence between NGLY1 and AQP1. From this statement, starts the logics to link this regulatory gene connections to the phenotypes of the disease. The following statement collects a list of all the human genes involved in the regulatory connections as _genes_:

 WITH COLLECT(DISTINCT ortholog_rna.id) + COLLECT(DISTINCT tf.id) AS genes

First, we look for all the genes in the graph that have an association of any type with the disease phenotypes. We keep those paths that match the NGLY1 Deficiency disease node, defining the node type _DISO_ and the node `id` attribute to _MONDO:0014109_ (the MONDO ID of the NGLY1 Deficiency), with its associated phenotypes using an open edge type to a disease phenotype node type with the _DISO_ label, and from these disease phenotypes we match those edges (open type) that relate the disease phenotypes to gene node types using the _GENE_ label. In this template we named the associated NGLY1 Deficiency disease phenotypes as 'ph' and their associated genes as 'g'.

 MATCH path=(:DISO {id: 'MONDO:0014109'})-->(ph:DISO)--(g:GENE)

Then, we apply the rule to filter the paths to only human genes that are in the regulatory hypothesis, i.e. in the `genes` list, and to specifically _human_ disease phenotypes, which are identified by the Human Phenotype ontology (HP) that uses the standard id prefix format annotation `HP`:

 WHERE g.id in genes AND ph.id CONTAINS 'HP:'

Last, we retrieve paths:

 RETURN path


{sp}+

==== Explore Hypotheses


The resulting gene-phenotype links can be graphically visualized such as:



{sp}+

[#img-q2-paths]
.Query 2 resulting hypotheses
[link=https://www.dropbox.com/s/xh14124tnqz0noo/query2_paths_v3.2.png?dl=0]
image::https://www.dropbox.com/s/xh14124tnqz0noo/query2_paths_v3.2.png?dl=1[height=500, width=3000]

{sp}+


or can be summarized as a table such as:


{sp}+


[#img-q2-table]
.Query 2 summary table
[link=https://www.dropbox.com/s/vksf6gccraaf92g/query2_table_v3.2.png?dl=0]
image::https://www.dropbox.com/s/vksf6gccraaf92g/query2_table_v3.2.png?dl=1[height=500, width=3000]


{sp}+

where the query should be:

**Execute the query to get a summary table of phenotypes and the gene list of genes associated with**

[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,ortholog_rna,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) contains 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

WITH COLLECT(DISTINCT ortholog_rna.id) + COLLECT(DISTINCT tf.id) AS genes

MATCH path=(:DISO {id: 'MONDO:0014109'})-->(ph:DISO)--(g:GENE)

WHERE g.id in genes AND ph.id CONTAINS 'HP:'

RETURN DISTINCT ph.id AS phenotype_id, ph.preflabel AS phenotype_label, COLLECT(DISTINCT g.name) AS gene_id_list, count(DISTINCT g.id) AS genes

ORDER BY genes DESC
----

{sp}+

IMPORTANT:  Click the top right **black&white** arrow to go to the next page


{sp}+



== Last remarks
//**Last wrapped up or conclusion section**


=== HYPOTHESIS DISCOVERY ON THE NGLY1 DEFICIENCY KNOWLEDGE GRAPH

//* structuring the current knowledge as a graph (structured reviews)
The NGLY1 Deficiency Knowledge Graph is a *structured review* of the current knowledge around this ultra-rare disease. Reviewing and structuring knowledge as a graph allows exploration of connections between entities otherwise difficult to query in one step and from only one endpoint. The user can explore explicit and implicit knowledge and check the supporting evidence. A graph allows to connect diverse types of nodes and relationships, different sources and importantly different domains and knowledge graphs. Some of the sophisticated queries that can be solved now are thanks to the heterogeneous information integrated from different topics (disease phenotypes, regulatory interactions or biological pathways) and sources (curated, animal, experimental evidence). We showcase how the graph can be interrogated to mine the gap of knowledge from gene to phenotype for instance. Integrating structured knowledge with omics data has the value to anchor data in a conceptual knowledge collection to aid interpretation and to facilitate discovery of new knowledge from experimental data in context. 


==== The neo4j browser application
The development of the NGLY1 Deficiency Knowledge Graph and its deployment into the Neo4j Browser application has been done to support researchers for *discovery*. The Neo4j Browser allows exploration through the Cypher query language, and visualization. Remarkably, Neo4j allows to store our graph and offers drivers for languages such as Python to communicate with the connected knowledge and data. Our pursuit is to review the current knowledge around a disease and to make it actionable for humans and machines to accelerate discoveries in the basic sciences for translational use. The knowledge and data landscape of the structured review cannot be extended interactively using the Neo4j Browser. The graph has also a community-editable http://100.25.145.12:8181/wiki/Main_Page[NGLY1 Deficiency *wikibase*] interface intended to _engage with the NGLY1 Deficiency research and patient&family community_ to expand the review with *expert knowledge*. Both graphs applications are synchronized to share the same content. Tracking biomedical with expert knowledge as a knowledge graph is a current strategy for knowledge-driven discovery and precision medicine. 

//** Neo4j allows to deploy a exploration and vizualization app
//** Neo4j via cypher allows exploration
//** Neo4j does not allow interactive edition of the graph


==== To wrap up
We offer a disease specific knowledge graph to represent and manage knowledge and data around the NGLY1 Deficiency, and a web interface application served by Neo4j for exploration and visualization:

- We integrated and represented existing knowledge and data as an open knowledge graph on the particular topic of a specific rare disease.
- The knowledge graph makes knowledge and data more accessible to the NGLY1 Deficiency community.
- The use of semantics makes the knowledge graph linkable and *interoperable* to other resources such as other rare disease knowledge graphs or the Linked Open Data.
- The knowledge graph allows to ingest crowdsourced statements from the NGLY1 Deficiency Wikibase.
- The knowledge graph allows graph processing to query entities, their relations to their attributes or to other entities, along with provenance and evidence of where that knowledge was obtained to enable evidence-based discovery for the NGLY1 Deficiency research community.
- Multi-dimensional complicated information can be now easily queried without specialized knowledge in an interactive way. Based on answers, users can take analysis in new directions and hypothesis.
- We deployed the knowledge graph on GitHub as flat files, and on an online Neo4j Browser to make it *findable*, *accessible* and *queryable-usable* and *re-usable*. 
- We implemented a Python library to make the creation and use of the knowledge graph more *reproducible*.
- We created a FAIR knowledge graph to the benefit of humans and machines.

=== MORE QUERIES
There are more examples of queries that can be done from traversing the graph looking for paths connecting entities to transcriptome review-expanded summaries. The user can see more examples in notebooks stored in the https://github.com/NuriaQueralt/ngly1-graph/tree/master/neo4j-graphs/ngly1-v3.2/cypher-queries[GitHub repository project - queries]. These notebooks are written in Python programming language and provide an alternative interface to query the graph that allows to tracking queries and results. 

For advanced exploration of the graph please, run the following command by clicking the box to access the advanced guide tutorial. 

[source,cypher]
----
:play http://ngly1graph.org:8001/guides/advanced.html
----

NOTE:  The user should have installed https://jupyter.org/[the Jupyter Notebook] software and the IPython kernel for Python3 to run and visualize the queries provided. 






