:author: NÃºria Queralt Rosinach
:twitter: nqueralt
:tags: NGLY1 Deficiency, NGLY1, Hypothesis Generation
:neo4j-version: 3.5.3
//:imagesdir: ./images


== NGLY1 Deficiency Knowledge Graph in Neo4j

The NGLY1 Deficiency Knowledge Graph is the first knowledge network for NGLY1 Deficiency, which is an ultra-rare genetic disease with neither a treatment nor a clear understanding of the underlying biology. It reviews the current knowledge and data on the NGLY1 Deficiency and it is represented as a single graph for exploration and discovery. Knowledge comes from biocuration of the scientific literature and retrieval from biomedical knowledge bases. This knowledge is integrated with omics data from Drosophila melanogaster model, an animal system that has helped the community understand a wide range of human conditions, from alcoholism to Alzheimer's disease and Rett's syndrome. The parallels between the genomes of Drosophila and humans, over about 60%, provides and opportunity to reveal basic genes or processes associated with disease. This is the Neo4j Web interface for the NGLY1 Deficiency Knowledge Graph to run queries and explore the structured reviewed knowledge and data. The current graph version v3.2 contains 237,027 relationships of 32 types among 9,365 nodes of 8 types (xx genes, xx pathways). The user is interacting with a read-only instance of the graph served by Neo4j, a database technology designed for graphs. For the writable version of the graph, the user should refer to http://100.25.145.12:8181/wiki/Main_Page[NGLY1 Deficiency wikibase instance] of the project, a collaborative and community-editable version of the graph.

{sp}+

//=== Explore reviewed knowledge for hypothesis discovery

=== Discover from the knowledge-data reviewed

The NGLY1 Deficiency Knowledge Graph is a network of biological and clinical relations offered as a knowledge resource to the research community to enable discovery on its cause and of its symptoms. The network is disease domain specific to NGLY1 deficiency and can be used for different research questions from the analysis of disease gene functions, the investigation of the genetic basis and underlying molecular biology of disease phenotypes, to the discovery of hypothesis or explanations linking concepts in the graph. The user can query the knowledge graph asking for paths between entities to discover new hypotheses or extracting data that can shed light or give new ideas. Exmples of the kind of questions the user can formulate are:

----
    1. What are the gene functions, gene interactions and cellular processes associated with the perturbed gene expression in Drosophila?
    2. What are the human homologs related to the perturbed fly transcriptome in the NGLY1 deficiency model?
    3. What are the most common human TFs associated with the genes in the fly transcriptome?
    4. Have the homolog genes in human or their gene interactors been associated with NGLY1 Deficiency phenotypes?
    5. Can we retrieve explanations or paths linking two entities of interest such as two genes? What are the most common path topologies linking these genes? Are there paths based on regulatory TF-target gene interactions linking two molecular entities of interest in the structured review?
----


{sp}+

=== Data model


The picture shows the data model or schema or meta-graph for NGLY1 Knowledge Graph v3.2. The semantics of the concepts in the graph rely on the semantic types that build the https://www.nlm.nih.gov/pubs/factsheets/umlssemn.html[UMLS Semantic Network]. To check the definition of each semantic type abbreviation see the https://metamap.nlm.nih.gov/Docs/SemGroups_2013.txt[UMLS Semantic Groups definitions].

[#img-schema]
.NGLY1 Deficiency Knowledge Graph v3.2 data model
[link=https://www.dropbox.com/s/6m1rfik5qqftyvi/ngly1kg_metagraph_2019-03-12.png?dl=0]
image::https://www.dropbox.com/s/6m1rfik5qqftyvi/ngly1kg_metagraph_2019-03-12.png?dl=1[float=center]
//My image dir path is {imagesdir} 

//image::metagraph.png[height=300,float=center]
//image::ngly1kg_metagraph_2019-03-12.png[height=300,float=center]
//image::https://github.com/NuriaQueralt/ngly1-neo4j-guides/blob/master/guides/images/ngly1kg_metagraph_2019-03-12.png[height=300,float=center]
//image::https://github.com/NuriaQueralt/ngly1-neo4j-guides/commit/30d901927193d59f624409bab8d329c91aa873a1?dl=1[height=300,float=center]



For an interactive description of the data model, run the following query:

[source,cypher]
----
CALL db.schema()
----



The network relates data of 8 types: genes, disease phenotypes, variants, genotypes, pathways, chemicals, anatomy, and organisms with different levels of association expressiveness. Data and relations are described using standard semantics: community-driven ontologies and controlled vocabularies to enable interoperability with external resources and reduce ambiguity.

{sp}+


=== Structure of the guide



This guide is structured with query pages in the next section, where the guide shows an explicit question and develops a type of query we can formulate to inspect hypotheses from the knowledge in the review. In the last section, the guide develops how to query the graph with some hints and examples to aid the new user. To see how to retrieve connections through regulation between NGLY1-AQP1 go to:


     ** _Slide 2_: *Data sources*

     * _Slide 3_: *Query to explore hypothesis relating NGLY1 and AQP1 genes* 

     * _Slide 4_: *Query to explore hypothesis relating AQP1 gene and NGLY1 Deficiency phenotypes* 

     * _Slide 5_: *Last remarks*



IMPORTANT:  Click the top right **black&white** arrow to go to the next page


== DATA 


We reviewed and structured information around the clinical profile and the pathophysiology of the NGLY1 deficiency to help biomedical researchers to discover evidence-based explanations for their hypotheses on its cause and of its symptoms. From free text [Lam et al ref], we structured around 100 associated disease phenotypes and formalized them into the HPO. The third release of the graph organizes curated information from human and animal models 1) manually extracted from the literature; 2) retrieved from https://monarchinitiative.org/[the Monarch Initiative]; 3) transcription factors and their target genes from several sources in tftargets and MSigDB; 4) manually contributed by the community from http://100.25.145.12:8181/wiki/Main_Page[the NGLY1 Deficiency wikibase platform]; and integrates all this heterogeneous knowledge with an experimental gene expression data set from a fruit fly _Drosophila melanogaster_ model of the disease [ref]. 

{sp}+



=== ORIGINAL DATA SOURCES


The NGLY1 Deficiency Knowledge Graph reviews information from recent investigations published in scientific journals, knowledge derived from animal models and data generated in high-throughput experiments. The integrated knowledge and data comes from different external sources including sciencitic literature, knowledge bases and experimental omics data sets and are annotated to its provenance and evidence. The integration is performed by means of vocabulary mapping and normalization. We provide a computational tool for the reproducible generation workflow of the graph, a Python library where each data source has a specific library to prepare edges as a network to be integrated into the NGLY1 Deficiency Knowledge Graph schema.


==== Curation Network
Data come from manual curation. It is retrieved from tab files generated by the biocurator. shape of the dataframe. 

==== Animal Model Network
Data come from the Monarch Initiative Knowledge Base. To translate genotype and phenotype, the knowledge base integrates animal model with human data such as diseases, genes, variants, or phenotypes. It is retrieved through the BioLink API. shape of the dataframe.


==== Regulation Network 
Data come from the tftargets and the MSigDB databases and consists of transcription factor and target genes known human associations. tftargets is. shape of the dataframe. MSigDB is. shape of the df. In total the regulation network is shape of the df.


==== Transcriptomics Network
Data come from an RNA-seq experiment on the perturbed transcriptome in a fly model of NGLY1 Deficiency [ref]. It contains the differential expression at cutoff . shape of the dataframe.



IMPORTANT: Click the top right **black&white** arrow to go to the next page
icon:angle-double-right[]Click the top right **black&white** arrow to go to the next page



== Explore reviewed knowledge for hypothesis discovery
//== QUERY 1: HYPOTHESIS DISCOVERY RELATING NGLY1 - AQP1 


In the following slides, we will query the graph to showcase how to explore this heterogeneous knowledge to extract potential hypotheses focusing on a specific research question.


{sp}+

=== RESEARCH QUESTION


Consider a researcher wants to determine whether a novel protein (Aquaporin 1, AQP1) is related with the NGLY1 Deficiency. From the experimental observation, there is evidence of an association between NGLY1 and AQP1 genes at expression level in both a mouse model and patient data. The researcher's hypothesis is that both genes are linked through transcriptional regulation and she wants to search the knowledge and the expression data integrated in the review to find potential mechanisms and, if so, to link these mechanisms to the disease phenotypes. To answer this complex question we will perform two different queries:

----
            1. First we will query for NGLY1-AQP1 genetic association evidence
            2. Then we will perform a second query to retrieve gene-disease association evidence
----


{sp}+

=== STYLE THE GRAPH


We first style the graph, before playing with it. To load the NGLY1 Deficiency graph style into the Neo4j Browser, run the following command by clicking the box, which will populate the shell, and hitting ```enter``` (or ```return``` in some keyboards). This command only needs to be run once per web browser.


[source,cypher]
----
:style http://100.25.145.12:8001/guides/style.grass
----


TIP:  The user can set up the style of the graph by itself locally in its browser. If you click on any label or relationship above the graph visualization, you can then choose its styling in the area below the graph. Colors, sizes, and captions are selectable from there. Click http://guides.neo4j.com/browser.html[Styling Neo4j Browser Visualization] for more details. 

{sp}+


=== QUERY 1: HYPOTHESIS DISCOVERY RELATING NGLY1 - AQP1


In particular, the researcher is interested in regulatory hypotheses that link NGLY1 and AQP1 through gene interactions. To mine the gap of knowledge we will explore for paths that link the perturbed transcriptomic expression on fly model of NGLY1 Deficiency through regulatory TF-target gene interactions to human AQP1. Since we are translating fly biology to human biology we will explore paths with a rule of 2-hop `gene-interacts with` to allow certain flexibility.


{sp}+

==== Define the query template


The first think to do is to formulate the query template in a structured manner to answer this question:

{sp}+

image::https://www.dropbox.com/s/qypkrn1l50p83n6/query1_topology_v3.2.png?dl=1[height=100, width=5000, align="center"]

{sp}+

In this query, we specify relations between genes to be of two types: 'interacts with' that will be explicitly either of regulatory origin once filtered by source of provenance to the databases *tftargets* and *MSigDB* or of gene expression origin once filtered by reference to the *PubMed Identifier*, where the experimental fly transcriptome data set was published, or a gene-gene interaction if none of these restrictions applied; and 'in 1 to 1 orthology relationship with' that relates to direct orthology between genes.


{sp}+

==== Formulate the query in Cypher


Once we have a clear query template we can create the query in Cypher to search for mechanistic links based on gene interactions and regulation edges. The following query traverses the graph to find these kind of links in the review:

[%autofit]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN path
----


Let's explain this query statement by statement. First, we look for mechanisms that match the query template above, where we define gene interactions with the node type _GENE_ and the edge types  _RO:0002434_ (interacts with) and _RO:HOM0000020_ (in 1 to 1 orthology relationship with):


 MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)


We define the source and target nodes with the identifier (id). The source is Pngl (FlyBase:FBgn0033050), which is the fly ortholog of the human gene NGLY1, (HGNC:17646) and the target is the human gene AQP1 (HGNC:633)


 WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633'


and we impose that every every node instance is traversed only once in the path.


 AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))


We extract some features from each path to then filter them


 WITH path,


We mark nodes with too general meaning


 [ n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype'] ] AS nodes_marked,


We mark regulatory edges through the "reference_supporting_text" edge provenance attribute


 [ r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*' ] AS interactions


We filter paths that only content specific node instances and at least have one regulatory edge


 WHERE size(nodes_marked) = 0 AND size(interactions) <> 0


We filter paths that the first edge, named in the path pattern as 'i1', is a transcriptomic interaction from the experimental dataset published in the reference id PMID:29346549. We filter it by publication provenance using the `reference_uri` edge attribute


 AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'


We filter paths that the fourth edge, named as 'i4', is a TF --> GENE regulatory interaction. We define a specific directionality of the interaction to keep the TF function towards AQP1. We filter it by source provenance using the `reference_supporting_text` edge attribute


 AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'


We retrieve paths


 RETURN path


{sp}+



=== EXPLORE HYPOTHESES


Results can be vizualized in a summary table of a list of properties calculated from all paths or in a graph exposing all the paths. In this case we can explore all paths directly in graph format that looks like:


image::https://www.dropbox.com/s/bbdg1orj7ztrvup/query1_paths_v3.2.png?dl=1[height=3000, width=10500]


If we want to retrieve a summary table with all gene interactors that are expressed gene interactors of Pngl and AQP1 Transcription Factors (TFs) ordered by the total number of paths they appear, the query should be:


 MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

 WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

 WITH path,rna,tf,

 [n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

 [r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

 WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

 AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

 AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

 RETURN DISTINCT rna.name AS Expressed_gene_name, tf.name AS TF_name, count(distinct path) as Total_paths

 ORDER BY Total_paths DESC


Which results in the following table:


image::https://www.dropbox.com/s/v7i3ld0x3cup7nj/query1_table_v3.2.png?dl=1[height=1000, width=9000]


TIP: If the user wants to list ortholog rna genes, replace `rna` label by `ortholog_rna` in the query to refer to the third node position in the query template.



{sp}+


=== TRY IT!


Execute the query yourself and explore results.


==== Query 1: paths that relate NGLY1 and AQP1 through gene interactions


_Query network_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN path
----


==== Query 2: summary table of gene interactors in the resulting paths


_Query network_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,rna,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN DISTINCT rna.name AS Expressed_gene_name, tf.name AS TF_name, count(distinct path) as Total_paths

ORDER BY Total_paths DESC
----



IMPORTANT:  Click the top right **black&white** arrow to go to the next page


== QUERY 2: HYPOTHESIS DISCOVERY RELATING AQP1 - DISEASE PHENOTYPES


Next question that the researcher may want to explore is the connection between the AQP1 and the NGLY1 Deficiency disease phenotypes.



{sp}+

=== QUERY

In particular, the researcher wants to query the review to extract paths from genes to phenotypes that link AQP1-NGLY1 genetic evidence with the NGLY1 Deficiency symptoms.


=== Define the query template


Again, the first think to do is to formulate the query template in a structured manner to answer this question:


image::https://www.dropbox.com/s/ch261h9xwpu5zm5/query2_topology_v3.2.png?dl=1[height=600, width=2500, align="center"]

This query is in two steps.


=== Formulate the query in Cypher


{sp}+


=== EXPLORE HYPOTHESES


The resulting paths can be visualized such as


image::https://www.dropbox.com/s/xh14124tnqz0noo/query2_paths_v3.2.png?dl=1[height=500, width=3000]


or can be summarized as a table such as


image::https://www.dropbox.com/s/vksf6gccraaf92g/query2_table_v3.2.png?dl=1[height=500, width=3000]


{sp}+


=== TRY IT!


==== Query 1


_Query network_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,ortholog_rna,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) CONTAINS 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

WITH COLLECT(DISTINCT ortholog_rna.id) + COLLECT(DISTINCT tf.id) AS genes

MATCH path=(:DISO {id: 'DOID:0060728'})-->(ph:DISO)--(g:GENE)

WHERE g.id in genes AND ph.id CONTAINS 'HP:'

RETURN path
----


==== Query 2


_Query network_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(rna:GENE)-[i2:`RO:HOM0000020`]-(ortholog_rna:GENE)-[i3:`RO:0002434`]-(tf:GENE)-[i4:`RO:0002434`]->(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,ortholog_rna,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked,

[r IN relationships(path) WHERE toLower(r.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'] AS interactions

WHERE size(nodes_marked) = 0 AND size(interactions) <> 0

AND toLower(i1.reference_uri) contains 'pubmed/29346549'

AND toLower(i4.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

WITH COLLECT(DISTINCT ortholog_rna.id) + COLLECT(DISTINCT tf.id) AS genes

MATCH path=(:DISO {id: 'DOID:0060728'})-->(ph:DISO)--(g:GENE)

WHERE g.id in genes AND ph.id CONTAINS 'HP:'

RETURN DISTINCT ph.id AS phenotype_id, ph.preflabel AS phenotype_label, COLLECT(DISTINCT g.name) AS gene_id_list, count(DISTINCT g.id) AS genes

ORDER BY genes DESC
----


IMPORTANT:  Click the top right **black&white** arrow to go to the next page


== LAST REMARKS
**Last wrapped up or conclusion section**


=== HYPOTHESIS DISCOVERY ON THE NGLY1 DEFICIENCY KNOWLEDGE GRAPH
Reviewing knowledge as a graph allows exploration of connections between entities otherwise difficult to query in one step and from only one endpoint. The user can explore knowledge and check the supporting evidence. A graph allow to connect diverse type of nodes and relationships, different domains.


=== MORE QUERIES
There are more examples of queries that can be done from traversing the graph looking for paths connecting entities to transcriptome review-expanded summaries. The user can see more examples in notebooks stored in the https://github.com/NuriaQueralt/ngly1-graph/tree/master/neo4j-graphs/ngly1-v3.2/cypher-queries[GitHub repository project - queries]. These notebooks are written in Python programming language and provide an alternative interface to query the graph that allows to tracking queries and results. 

For advanced exploration of the graph please, run the following command by clicking the box to access the advanced guide tutorial. 

[source,cypher]
----
:play http://100.25.145.12:8001/guides/advanced.html
----

NOTE:  The user should have installed https://jupyter.org/[the Jupyter Notebook] software and the IPython kernel for Python3 to run and visualize the queries provided. 


IMPORTANT:  Click the top right **black&white** arrow to go to the next page




