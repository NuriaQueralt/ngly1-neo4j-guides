:author: NÃºria Queralt Rosinach
:twitter: nqueralt
:tags: NGLY1 Deficiency, NGLY1, Hypothesis Generation
:neo4j-version: 3.0.3




== HOW TO QUERY

=== Exploring with Neo4j


https://neo4j.com/[Neo4j] is a graph database management system developed by Neo Technology, Inc. A https://en.wikipedia.org/wiki/Graph_database[graph database] is a database that uses graph structures for semantic queries with nodes, edges and properties to represent and store data. https://en.wikipedia.org/wiki/Cypher_Query_Language[Cypher] is the query language for Neo4j's graphs. To follow this guide you should be able to read and understand Cypher queries.

You are currently interacting with the NGLY1 Deficiency knowledge graph through the Neo4j Browser, which provides a web interface to the database where you can run queries and explore the connected knowledge. To explore the whole NGLY1 knowledge graph, click on the following block to copy-paste the query into the shell and run it:

[source,cypher]
----
MATCH path=(n)--() RETURN path LIMIT 10
----

{sp}+

IMPORTANT:  Click the top right **black&white** arrow to go to the next page




== THE SEMANTICS OF THE NGLY1 DEFICIENCY KNOWLEDGE GRAPH v3.2


In this page we provide the data model of our semantic graph and some example queries to retrieve data model information from the graph.


=== Data model


The data model is the abstraction of how knowledge is represented in the graph and it is necessary to run more interesting queries. The following picture shows the data model or schema for NGLY1 Knowledge Graph v3.0. The semantics of the concepts in the graph rely on the semantic types that build the https://www.nlm.nih.gov/pubs/factsheets/umlssemn.html[UMLS Semantic Network]. To check the definition of each semantic type abbreviation see the https://metamap.nlm.nih.gov/Docs/SemGroups_2013.txt[UMLS Semantic Groups definitions].


[#img-schema]                                                                                                           
.NGLY1 Deficiency Knowledge Graph v3.2 data model                                                                       
[link=https://www.dropbox.com/s/6m1rfik5qqftyvi/ngly1kg_metagraph_2019-03-12.png?dl=0]                                  
image::https://www.dropbox.com/s/6m1rfik5qqftyvi/ngly1kg_metagraph_2019-03-12.png?dl=1[float=center]   
//image::https://www.dropbox.com/s/6p1zykpuexslx8e/ngly1_graph_schema.png?dl=1[height=300,float=center]
//image::images/ngly1_graph_schema.svg[height=300,float=center]

{sp}+ 

=== Retrieve information about the data model


To retrieve the explicit semantics to use for node types and its attributes in the graph, run the following query:


[source,cypher]                                                                                                         
----                                                                                                                    
MATCH (n)                                                                                                               
RETURN DISTINCT labels(n) AS node_type,                                                                                              
count(*) AS NumberOfEntities, reduce(keys = [], keys_n in collect(keys(n)) | keys + filter(k in keys_n WHERE NOT k IN keys)) AS EntityAttributes 
ORDER BY NumberOfEntities DESC                                                                                          
---- 


To retrieve the explicit semantics to use for edge types and their attributes, run the following query:


[source,cypher]                                                                                                         
----                                                                                                                    
MATCH ()-[r]-()                                                                                                         
RETURN DISTINCT type(r) AS relationship_type, r.property_label AS relationship_label,                                                                                                
count(*) AS NumberOfRelationships, reduce(keys = [], keys_r in collect(keys(r)) | keys + filter(k in keys_r WHERE NOT k IN keys)) AS EntityAttributes 
ORDER BY NumberOfRelationships DESC                                                                                     
----    


To retrieve what is related and how, run the following query:


[source,cypher]                                                                                                         
----                                                                                                                    
MATCH (n)                                                                                                               
MATCH (n)-[r]->(m)                                                                                                      
WITH n, type(r) AS via, m                                                                                               
RETURN labels(n) AS from, via, labels(m) AS to, count(*) AS freq                                                        
ORDER BY freq DESC                                                                                                      
---- 


{sp}+ 

IMPORTANT:  Click the top right **black&white** arrow to go to the next page


== HINTS



=== How to extract hypotheses


Hypotheses are derived from exploring the evidence connecting a source-target pair of nodes. To explore this evidence we traverse the NGLY1 Knowledge Graph running queries. Hypotheses are extracted querying for semantic paths connecting source and target nodes. In the next pages, we show examples of hypotheses extracted by specific queries between two nodes. Each page is dedicated to a source-target pair and we provide several query templates to illustrate and guide the user how to query the graph to extract hypotheses. To access these pairwise hypotheses pages the user has to click the b/n arrow on the top-right part of the web. For instance, if we click this arrow we access the first page that is dedicated to the NGLY1 Deficiency-NGLY1 pair. In this page we provide examples on how to run open queries to retrieve hypotheses of path length not longer than 2 edges, i.e. without specifying a explicit pattern connecting two nodes, using or not filters. 


=== How to query the graph


To query the graph we have to use the Cypher query language. This language allows us to query nodes, node attributes, relationships between nodes and attributes of these relationships. We can perform open queries (without a specific query template), which are usually computationally less efficient for large graphs. Alternatively, we can be more specific and search for explicit nodes and connection patterns using semantics and a query template, which makes queries more efficient in large data graphs. The data model shows how things are meaningfully represented and related to each other and it is the base to use semantics in the query. To illustrate how to query for specific semantic connections the NGLY1 Knowledge Graph, we provide some examples in Cypher using specific query templates. We do not provide a tutorial for the Cypher language. To learn how to query using Cypher the user should refer to external sources like https://neo4j.com/developer/cypher-query-language/[The Neo4j's introduction to Cypher]


{sp}+

IMPORTANT:  Click the top right **black&white** arrow to go to the next page

== QUERIES FOR GRAPH TRAVERSAL ANALYSIS. QUERY EXAMPLE: NGLY1 --- AQP1 PATHS

To solve a question we explore and search the review by querying the graph. Let's develop the whole process with a query example.


=== Open query: extraction of metapaths
First, we query how many paths link NGLY1 and AQP1 human genes in the review with an open query, i.e. no specific query template defined:

_Open query_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(:GENE)-[*..3]-(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0 AND toLower(i1.reference_uri) contains 'pubmed/29346549'

RETURN count(distinct path) as paths
----


As a result, the review provides 6670 linking paths or hypotheses. Let's retrieve a summary table with the most common meta-pahts or the semantics of these 6670 paths:

_Meta-paths_

[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(:GENE)-[*..3]-(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0 AND toLower(i1.reference_uri) contains 'pubmed/29346549'

RETURN DISTINCT extract (x in rels(path) | type(x)) as types, extract (n in nodes(path) | labels(n)) as labels, length(path) as mp_length, count(distinct path) as paths

ORDER BY mp_length, paths DESC
----

The meta-path information is useful to refine the query template to a meaningful mechanism and to get less than (and more visually explorable) 1000 paths.

=== Query template: query a specific query template semantics 

To structure the query in Cypher, it is important to first formulate the question in natural language and then translate it in a query template:

****
# Find source and target gene interactors that are expressed in the same tissue or cell type:

``source:GENE)-[i1:`RO:0002434`]->(:GENE)-[:`RO:0002206`]-(:ANAT)-[:`RO:0002206`]-(:GENE)-[:`RO:0002434`]-(target:GENE)``
****


==== Query in Cypher

Once we have a clear idea of the query template, it is easier to formulate it in a structured way in Cypher:


_Query template_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(:GENE)-[:`RO:0002206`]-(:ANAT)-[:`RO:0002206`]-(:GENE)-[:`RO:0002434`]-(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0 AND toLower(i1.reference_uri) contains 'pubmed/29346549'

RETURN count(distinct path) as paths
----




==== Results
The query gives 3150 paths. This result is not visually explorable since the number of paths is larger than 1000. But we can summarize interesting information from these 3150 paths in a table. See the following summary table as example:


_Summary Table_

[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(:GENE)-[:`RO:0002206`]-(:ANAT)-[:`RO:0002206`]-(:GENE)-[:`RO:0002434`]-(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0 AND toLower(i1.reference_uri) contains 'pubmed/29346549'

RETURN DISTINCT extract(x in rels(path) | x.property_label) AS types, extract(n in nodes(path) | n.name) AS labels, length(path) AS mp_length, count(distinct path) AS paths

ORDER BY mp_length, paths DESC
----



=== Refine query template: refine the query to get visually explorable hypotheses (> 1000 paths)
Let's refine the semantics and rules of the query to visualize meaningful mechanistic hypotheses. The refined query template is:

****
# Find source and target gene interactors that are linked by orthology:

``(source:GENE)-[i1:`RO:0002434`]->(:GENE)-[:`RO:HOM0000017`]-(:GENE)-[:`RO:0002434`]-(:GENE)-[:`RO:0002434`]-(target:GENE)``
****


==== Query in Cypher

The query in Cypher is:


_Refine query template_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(:GENE)-[:`RO:HOM0000017`]-(:GENE)-[:`RO:0002434`]-(:GENE)-[:`RO:0002434`]-(target:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND target.id = 'HGNC:633' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0 AND toLower(i1.reference_uri) contains 'pubmed/29346549'

RETURN count(distinct path) as paths
----



==== Results

This query retrieves 114 paths. The results appear below the command shell as a graph or as a table. Results are only exportable as files: graphic (PNG, SVG, ..) or structured (CVS, ..) formats. 

WARNING:   The current Neo4j Browser does not provide ``session`` services, thus there is not option to save any session.



{sp}+

IMPORTANT:  Click the top right **black&white** arrow to go to the next page

== QUERIES FOR TRANSCRIPTOME ANALYSIS
In this page we give some query examples to analyze the transcriptomic profile integrated in the review.

=== Query for common TFs
These queries retrieve information about the most common or frequent transcription factors (TFs):

_Summarize most common TFs_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(ortho:GENE)-[iortho]-(gene:GENE)<-[i2:`RO:0002434`]-(tf:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,iortho,i2,tf,gene,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0

AND toLower(i1.reference_uri) =~ '.*pubmed/29346549.*'

AND toLower(iortho.property_label) CONTAINS 'orthology'

AND toLower(i2.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN DISTINCT tf.id as TF_id, tf.preflabel as TF_symbol, tf.name as TF_name, tf.description as TF_description, count(distinct gene.preflabel) as Freq

ORDER BY Freq DESC
----


_Summarize frequency and target gene sets for each TF_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(o:GENE)-[iortho]-(g:GENE)<-[i2:`RO:0002434`]-(tf:GENE)

WHERE source.id = 'FlyBase:FBgn0033050' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,iortho,i2,g,tf,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0

AND toLower(i1.reference_uri) =~ '.*pubmed/29346549.*'

AND toLower(iortho.property_label) CONTAINS 'orthology'

AND toLower(i2.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

RETURN DISTINCT tf.preflabel as TF_symbol, tf.name as TF_name, COLLECT(DISTINCT g.preflabel) as geneset, count(distinct g.preflabel) as genes

ORDER BY genes DESC
----



{sp}+

=== Query for TF Pathways
The following query template is used to retrieve cellular pathways for each TF. Pathways are associated with the TF target genes:

image::https://www.dropbox.com/s/kvty5e0fuoqdadh/query3_topology_v3.2.png?dl=1[height=1000, width=5000, align="center"]

The query in Cypher:

_TF pathways_


[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(o:GENE)-[iortho]-(g:GENE)<-[i2:`RO:0002434`]-(tf:GENE), (g:GENE)-[i]-(pw:PHYS)

WHERE source.id = 'FlyBase:FBgn0033050' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,iortho,i2,g,tf,i,pw,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0

AND toLower(i1.reference_uri) =~ '.*pubmed/29346549.*'

AND toLower(iortho.property_label) CONTAINS 'orthology'

AND toLower(i2.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

AND toLower(i.property_label) <> 'enables'

RETURN DISTINCT tf.preflabel as TF_symbol, tf.name as TF_name,
                collect(DISTINCT g.preflabel) as geneset, count(distinct g.preflabel) as genes,
                collect(DISTINCT pw.preflabel) as pathway, count(distinct pw.preflabel) as pathways 
                                                                                                                                            ORDER BY genes DESC
----


{sp}+

=== Query for TF GO annotations: Cellular function
The following query in Cypher retrieves cellular functions for each TF. It follows the same logics as the prior query. Cellular functions are associated with the TF target genes:

_TF cellular functions_

[source,cypher]
----
MATCH path=(source:GENE)-[i1:`RO:0002434`]->(o:GENE)-[iortho]-(g:GENE)<-[i2:`RO:0002434`]-(tf:GENE), (g:GENE)-[i]-(pw:PHYS)

WHERE source.id = 'FlyBase:FBgn0033050' AND ALL(x IN nodes(path) WHERE single(y IN nodes(path) WHERE y = x))

WITH path,i1,iortho,i2,g,tf,i,pw,

[n IN nodes(path) WHERE n.preflabel IN ['cytoplasm','cytosol','nucleus','metabolism','membrane','protein binding','visible','viable','phenotype']] AS nodes_marked

WHERE size(nodes_marked) = 0

AND toLower(i1.reference_uri) =~ '.*pubmed/29346549.*'

AND toLower(iortho.property_label) CONTAINS 'orthology'

AND toLower(i2.reference_supporting_text) =~ '.*tftargets.*|.*msigdb.*'

AND toLower(i.property_label) = 'enables'

RETURN DISTINCT tf.preflabel as TF_symbol, tf.name as TF_name,
                collect(DISTINCT g.preflabel) as geneset, count(distinct g.preflabel) as genes,
                collect(DISTINCT pw.preflabel) as cellular_function, count(distinct pw.preflabel) as cellular_functions
                                                                                                                                        ORDER BY genes DESC
----
